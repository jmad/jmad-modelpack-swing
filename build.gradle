buildscript {
    project.ext.CERN_VM = System.getProperty('CERN_TECHNET_VM') ?: System.getenv('CERN_TECHNET_VM') ?: false
    project.ext.DEPLOYMENT = System.getProperty('deployment') ?: false
    project.ext.VCS_TAG = System.getProperty('TRAVIS_TAG') ?: System.getenv('TRAVIS_TAG')
    project.ext.POM = [
            groupId    : 'io.jmad',
            artifactId : 'jmad-modelpack-swing',
            description: 'Swing GUI components for accessing the JMad model package service.'
    ]
    project.ext.INFO = [
            repo            : 'https://github.com/jmad/jmad-modelpack-fx.git',
            url             : 'https://jmad.github.io/',
            github          : 'https://github.com/jmad/jmad-modelpack-fx',
            githubIssues    : 'https://github.com/jmad/jmad-modelpack-fx/issues',
            licenseNameShort: 'Apache-2.0',
            licenseName     : 'The Apache License, Version 2.0',
            licenseUrl      : 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    ]
    project.ext.BINTRAY = [
            repo        : 'jmad-repo',
            name        : 'jmad-modelpack-swing',
            organization: 'jmad',
            userName    : System.getProperty('BINTRAY_USER') ?: System.getenv('BINTRAY_USER'),
            apiToken    : System.getProperty('BINTRAY_API_TOKEN') ?: System.getenv('BINTRAY_API_TOKEN')
    ]

    repositories {
        if (CERN_VM) {
            maven { url 'http://artifactory.cern.ch/gradle-plugins' }
            maven { url 'http://artifactory.cern.ch/ds-jcenter' }
            maven { url 'http://artifactory.cern.ch/development' }
        } else {
            mavenCentral()
            maven { url 'https://repo.spring.io/plugins-release' }
            maven { url 'https://plugins.gradle.org/m2/' }
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.jk1:gradle-license-report:1.5'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'jmad'

sourceCompatibility = JavaVersion.VERSION_1_8

if (DEPLOYMENT) {
    println 'Applying deployment scripts'
    apply from: './scripts/bintray-deploy.gradle'
}

repositories {
    if (CERN_VM) {
        maven { url 'http://artifactory.cern.ch/ds-jcenter' }
        maven { url 'http://artifactory.cern.ch/development' }
    } else {
        mavenCentral()
        jcenter()
    }
}

dependencies {
    compile group: 'io.jmad', name: 'jmad-modelpack-service', version: '0.7.0'
    compile group: 'com.google.guava', name: 'guava', version: '28.1-jre'

    compile group: 'org.springframework', name: 'spring-core', version: '5.2.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-context', version: '5.2.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-beans', version: '5.2.0.RELEASE'

    compile group: 'io.projectreactor', name: 'reactor-core', version: '3.3.0.RELEASE'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compile group: 'org.swinglabs', name: 'swingx', version: '1.6.1'

    testCompile group: 'org.springframework', name: 'spring-test', version: '5.2.0.RELEASE'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.13.2'
    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
}

wrapper {
    gradleVersion = '5.4.1'
    distributionUrl = distributionUrl.replace("bin", "all")
}

sourceSets {
    main {
        java { srcDirs = ['src/java'] }
        resources { srcDirs = ['src/java'] }
    }

    test {
        java { srcDirs = ['src/test'] }
        resources { srcDirs = ['src/test'] }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        html.enabled true
        csv.enabled false
    }
}

javadoc { options.encoding = "UTF-8" }

if(!project.tasks.findByName("javadocJar")) {
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }
}

if(!project.tasks.findByName("sourcesJar")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

if (!project['CERN_VM']) {
    println 'Applying licensing report'
    apply plugin: 'com.github.jk1.dependency-license-report'

    licenseReport {
        renderers = [this.class.classLoader.loadClass('com.github.jk1.license.render.InventoryHtmlReportRenderer').newInstance()]
        filters = [this.class.classLoader.loadClass('com.github.jk1.license.filter.LicenseBundleNormalizer').newInstance()]
    }
}
